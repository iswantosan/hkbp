plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.app.hkbp"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.app.hkbp"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                def keystoreFile = keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                if (keystoreFile != null && keystoreFile.exists()) {
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                    storeFile keystoreFile
                    storePassword keystoreProperties['storePassword']
                }
            }
        }
    }

    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                def keystoreFile = keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                if (keystoreFile != null && keystoreFile.exists()) {
                    signingConfig = signingConfigs.release
                } else {
                    println "WARNING: Keystore file not found at ${keystoreFile?.absolutePath}. Using debug signing."
                    signingConfig = signingConfigs.debug
                }
            } else {
                println "WARNING: key.properties not found. Using debug signing for release build."
                signingConfig = signingConfigs.debug
            }
        }
    }
}

dependencies {
    // Firebase Messaging untuk Android native
    implementation 'com.google.firebase:firebase-messaging:23.4.1'
    // Core library desugaring untuk flutter_local_notifications
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}

flutter {
    source = "../.."
}
